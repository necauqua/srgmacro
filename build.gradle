plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

group = 'dev.necauqua.srgmacro'
version = '1.0.0'

pluginBundle {
    website = 'https://github.com/necauqua/srgmacro#readme'
    vcsUrl = 'https://github.com/necauqua/srgmacro.git'
    tags = ['minecraft', 'forge', 'mcp', 'srg', 'modding', 'obfuscation']
}

gradlePlugin {
    plugins {
        plugin {
            id = 'dev.necauqua.srgmacro'
            displayName = 'SRG macro'
            description = 'Replaces code that looks like srg("some MCP name") ' +
                    'with a string literal containing the appropriate SRG name for that item'
            implementationClass = 'dev.necauqua.srgmacro.SrgMacroPlugin'
        }
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://files.minecraftforge.net/maven' }
}

configurations {
    deployer
}

dependencies {
    implementation 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    deployer 'org.apache.maven.wagon:wagon-ssh-external:3.4.0'
}

if (project.hasProperty('repo.url')) {
    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployer
            repository(url: project['repo.url']) {
                authentication(userName: project['repo.username'], privateKey: project['repo.sk'])
            }
        }
    }
}
